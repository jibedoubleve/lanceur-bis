; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;----------------------------------------------------------------
;-- These variables should be defined in command line 
;-- /D<name>[=<value>] 	Emulate #define public <name> <value>
;----------------------------------------------------------------
;#define public MyAppVersion 
;#define public BinDirectory 
;----------------------------------------------------------------

#define MyAppName "Lanceur 2"
#define MyAppPublisher "JB Wautier"
#define MyAppURL "https://github.com/jibedoubleve/lanceur-bis"
#define MyAppExeName "Lanceur.Ui.WPF.exe"
#define ScheduledTaskName "Autorun Lanceur 2"

[Setup]
PrivilegesRequired=admin
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{73909BA2-D53C-4C49-9C0D-4E38F25148BC}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=lanceur.{#MyAppVersion}.setup
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; \
    Description: "{cm:CreateDesktopIcon}"; \
    GroupDescription: "{cm:AdditionalIcons}"; \
    Flags: unchecked

; Choose how the application starts up (radio button)
Name: "starts_cheduled_task"; \
    Description: "Start the application via a scheduled task"; \
    GroupDescription: "Startup options"; \
    Flags: exclusive unchecked

Name: "start_registry"; \
    Description: "Start the application via the registry"; \
    GroupDescription: "Startup options"; \
    Flags: exclusive unchecked

[Registry]
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; \
    ValueType: string; \
    ValueName: "Lanceur 2"; \
    ValueData: """{app}\{#MyAppExeName}"""; \
    Tasks: start_registry; \
    Flags: uninsdeletevalue

[Run]
Filename: "cmd"; \
    Parameters: "/C schtasks /Create /F /SC ONLOGON /TN ""{#ScheduledTaskName}"" /TR ""'{app}\{#MyAppExeName}'"""; \
    Tasks: starts_cheduled_task; \
    Flags: runhidden

[UninstallRun]
Filename: "cmd"; \
    Parameters: "/C schtasks /Delete /TN ""{#ScheduledTaskName}"" /F"; \
    RunOnceId: "delete_schedule_task"; \
    Tasks: starts_cheduled_task; \
    Flags: runhidden runascurrentuser

[Files]
Source: "{#BinDirectory}{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#BinDirectory}*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

